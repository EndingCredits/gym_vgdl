BasicGame
    SpriteSet
        floor > Immovable img=oryx/floor3
        flower > SpawnPoint stype=bee    prob=0.02 color=PINK img=oryx/cloak3
        hell   > 
            fastHell   > SpawnPoint stype=zombie prob=0.05 color=RED img=oryx/tombstone1
            slowHell   > SpawnPoint stype=zombie prob=0.03 color=RED img=oryx/tombstone1
        honey  > Resource color=GOLD img=oryx/heart1 limit=10
        moving >
            avatar > MovingAvatar healthPoints=1 limitHealthPoints=10 frameRate=8 img=oryx/swordman1_0
            bee    > RandomNPC speed=1   cooldown=3  color=YELLOW img=oryx/priest1
            zombie > Chaser stype=avatar cooldown=6 speed=0.5 color=BROWN img=newset/zombie
        wall > Immovable autotiling=true img=oryx/wall3

    InteractionSet
        honey avatar    > collectResource scoreChange=1
        honey avatar    > killSprite
        moving wall     > stepBack
        avatar zombie   > killIfHasLess resource=honey limit=1 scoreChange=-1
        avatar zombie   > changeResource resource=honey value=-1
        zombie avatar   > killSprite
        bee zombie      > transformTo stype=honey
        zombie bee      > killSprite
        avatar hell     > killSprite

    TerminationSet
        Timeout limit=1000 win=True
        SpriteCounter stype=avatar limit=0 win=False

    LevelMapping
        0 > floor flower
        1 > floor slowHell
        2 > floor fastHell
        . > floor honey
        - > floor zombie
        + > floor
        A > floor avatar
        w > wall
